#extension GL_GOOGLE_include_directive: require
#extension GL_EXT_ray_tracing: require
#extension GL_EXT_shader_16bit_storage: require
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#extension GL_EXT_shader_explicit_arithmetic_types_int16: require
#extension GL_EXT_nonuniform_qualifier: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64: require
#extension GL_EXT_buffer_reference: require

#include "rp_main_payload.glsl"
#include "rp_main_descriptors.glsl"

layout(location = PAYLOAD_INDEX_SHADE) rayPayloadInEXT ShadeRayPayload rayPayload;

#if MEDIUM_STACK_SIZE > 0
void stepVolume(uint mediumIdx)
{
    float distance = gl_RayTmaxEXT;

    Medium m = rayPayload.media[mediumIdx - 1];

    vec3 transmittance = exp(m.sigma_t * -distance);

    vec3 density = m.sigma_t * transmittance;

    float pdf = dot(rayPayload.walkSegmentPdf, density);

    rayPayload.throughput *= (m.sigma_s * transmittance) / pdf;

    rayPayload.ray_origin += gl_WorldRayDirectionEXT * distance;

    rayPayload.bitfield |= SHADE_RAY_PAYLOAD_VOLUME_WALK_MISS_FLAG;
    shadeRayPayloadIncrementWalk(rayPayload);
}
#endif

// Optimized implementation from GLM with only cross products:
// https://github.com/g-truc/glm/blob/47585fde0c49fa77a2bf2fb1d2ead06999fd4b6e/glm/detail/type_quat.inl#L356-L363
vec3 quatRotateDir(vec4 q, vec3 dir)
{
    vec3 a = cross(q.xyz, dir);
    vec3 b = cross(q.xyz, a);
    return dir + ((a * q.w) + b) * 2.0;
}

vec3 sampleDomeLight(uint domeLightIndex, vec3 rayDir)
{
    float u = (atan(rayDir.z, rayDir.x) + 0.5 * PI) / (2.0 * PI);
    float v = 1.0 - acos(rayDir.y) / PI;

    const uint lodLevel = 0;
    return textureLod(sampler2D(textures_2d[nonuniformEXT(domeLightIndex)], tex_sampler), vec2(u, v), lodLevel).rgb;
}

void main()
{
#if MEDIUM_STACK_SIZE > 0
    uint mediumIdx = shadeRayPayloadGetMediumIdx(rayPayload);

    if (mediumIdx > 0)
    {
        stepVolume(mediumIdx);

        return;
    }
#endif

    rayPayload.bitfield |= SHADE_RAY_PAYLOAD_TERMINATE_FLAG;

#if AOV_ID != AOV_ID_COLOR
    return;
#endif

    bool useFallbackDomeLight = false;

#ifndef DOME_LIGHT_CAMERA_VISIBLE
    bool isPrimaryRay = (rayPayload.bitfield & SHADE_RAY_PAYLOAD_BOUNCES_MASK) == 0;

    useFallbackDomeLight = isPrimaryRay;
#endif

    uint domeLightIndex = useFallbackDomeLight ? 0 : 1;
    vec3 sampleDir = normalize(quatRotateDir(PC.domeLightRotation, gl_WorldRayDirectionEXT));
    vec3 radiance = sampleDomeLight(domeLightIndex, sampleDir) * PC.domeLightEmissionMultiplier;

    rayPayload.radiance += rayPayload.throughput * radiance;
}
