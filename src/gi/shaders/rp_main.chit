#extension GL_GOOGLE_include_directive: require
#extension GL_EXT_ray_tracing: require
#extension GL_EXT_nonuniform_qualifier: enable
#extension GL_EXT_samplerless_texture_functions: require
#extension GL_EXT_shader_16bit_storage: require
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#extension GL_EXT_shader_explicit_arithmetic_types_int16: require
#extension GL_EXT_nonuniform_qualifier: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64: require
#extension GL_EXT_buffer_reference: require

#include "mdl_types.glsl"
#include "rp_main_descriptors.glsl"

#include "mdl_interface.glsl"
#include "mdl_shading_state.glsl"
#include "rp_main_payload.glsl"

vec3 mdl_absorption_coefficient(in State state);
bool mdl_thin_walled(in State state);
vec3 mdl_edf_emission_intensity(in State state);
void mdl_edf_emission_init(inout State state);
void mdl_edf_emission_sample(inout Edf_sample_data sret_ptr, in State state);
void mdl_edf_emission_pdf(inout Edf_pdf_data sret_ptr, in State state);
void mdl_edf_emission_evaluate(inout Edf_evaluate_data sret_ptr, in State state);
void mdl_bsdf_scattering_init(inout State state);
void mdl_bsdf_scattering_sample(inout Bsdf_sample_data sret_ptr, in State state);
void mdl_bsdf_scattering_pdf(inout Bsdf_pdf_data sret_ptr, in State state);
void mdl_bsdf_scattering_evaluate(inout Bsdf_evaluate_data sret_ptr, in State state);

layout(location = PAYLOAD_INDEX_SHADE) rayPayloadInEXT ShadeRayPayload rayPayload;

hitAttributeEXT vec2 baryCoord;

const uint TOTAL_LIGHT_COUNT = SPHERE_LIGHT_COUNT + DISTANT_LIGHT_COUNT + RECT_LIGHT_COUNT + DISK_LIGHT_COUNT;

bool russian_roulette(in float random_float, inout vec3 throughput)
{
    float max_throughput = max(throughput.r, max(throughput.g, throughput.b));
    float p = min(max_throughput, PC.rrInvMinTermProb);

    if (random_float > p)
    {
        return true;
    }

    throughput /= p;

    return false;
}

void sampleLight(vec4 k4, vec3 surfacePos, out vec3 dirToLight, out float dist, out vec3 power, out float invPdf, out uint diffuseSpecularPacked)
{
#if SPHERE_LIGHT_COUNT > 0
    if ((k4.x * TOTAL_LIGHT_COUNT) <= SPHERE_LIGHT_COUNT)
    {
        uint lightIndex = min(uint(k4.y * SPHERE_LIGHT_COUNT), SPHERE_LIGHT_COUNT - 1);

        SphereLight light = sphereLights[lightIndex];

        // TODO: sample solid angle of sphere light
        // https://schuttejoe.github.io/post/arealightsampling/
        vec3 samplePos = light.pos + sample_sphere(k4.zw, light.radiusXYZ);
        vec3 dir = samplePos - surfacePos;
        dist = length(dir);
        dirToLight = dir / dist;

        // https://graphics.cg.uni-saarland.de/courses/ris-2021/slides/03_ProbabilityTheory_MonteCarlo.pdf s.35
        vec3 lightNormal = normalize(samplePos - light.pos);
        float cosTheta = max(0.0, dot(-dirToLight, lightNormal));
        invPdf = ((light.area > 0.0) ? (light.area * cosTheta) : 1.0) / (dist * dist);

        power = light.baseEmission * PC.lightIntensityMultiplier;
        diffuseSpecularPacked = light.diffuseSpecularPacked;
    }
#endif
#if DISTANT_LIGHT_COUNT > 0
    else if ((k4.x * TOTAL_LIGHT_COUNT) <= (SPHERE_LIGHT_COUNT + DISTANT_LIGHT_COUNT))
    {
        uint lightIndex = min(uint(k4.y * DISTANT_LIGHT_COUNT), DISTANT_LIGHT_COUNT - 1);

        DistantLight light = distantLights[lightIndex];

        dist = 100000.0;
        dirToLight = -light.direction;
        power = light.baseEmission * PC.lightIntensityMultiplier;
        invPdf = light.invPdf;
        diffuseSpecularPacked = light.diffuseSpecularPacked;

        if (light.angle > 0.0)
        {
            vec3 t1, t2;
            orthonormal_basis(dirToLight, t1, t2);

            // https://math.stackexchange.com/questions/56784/generate-a-random-direction-within-a-cone/182936#182936
            float phi = (k4.z * 2 * PI) - PI;
            float theta = k4.w * light.angle;
            dirToLight = normalize(sin(theta) * (cos(phi) * t1 + sin(phi) * t2) + cos(theta) * dirToLight);
        }
    }
#endif
#if RECT_LIGHT_COUNT > 0
    else if ((k4.x * TOTAL_LIGHT_COUNT) <= (SPHERE_LIGHT_COUNT + DISTANT_LIGHT_COUNT + RECT_LIGHT_COUNT))
    {
        uint lightIndex = min(uint(k4.y * RECT_LIGHT_COUNT), RECT_LIGHT_COUNT - 1);

        RectLight light = rectLights[lightIndex];

        // TODO: solid angle sampling
        vec2 sampleOnRect = (k4.zw - vec2(0.5)) * vec2(light.width, light.height);

        vec3 t0 = decode_direction(light.tangentFramePacked.x);
        vec3 t1 = decode_direction(light.tangentFramePacked.y);
        vec3 samplePos = light.origin + sampleOnRect.x * t0 + sampleOnRect.y * t1;

        vec3 dir = samplePos - surfacePos;
        dist = length(dir);
        dirToLight = dir / dist;

        vec3 lightNormal = cross(t1, t0); // light forward/default dir is -Z (like UsdLux)
        float cosTheta = max(0.0, dot(-dirToLight, lightNormal));
        float area = light.width * light.height;
        invPdf = ((area > 0.0) ? (area * cosTheta) : 1.0) / (dist * dist);

        power = light.baseEmission * PC.lightIntensityMultiplier;
        diffuseSpecularPacked = light.diffuseSpecularPacked;
    }
#endif
#if DISK_LIGHT_COUNT > 0
    else
    {
        uint lightIndex = min(uint(k4.y * DISK_LIGHT_COUNT), DISK_LIGHT_COUNT - 1);

        DiskLight light = diskLights[lightIndex];
        vec2 radiusXY = vec2(light.radiusX, light.radiusY);

        vec2 sampleOnDisk = sample_disk(k4.zw, radiusXY);

        vec3 t0 = decode_direction(light.tangentFramePacked.x);
        vec3 t1 = decode_direction(light.tangentFramePacked.y);
        vec3 samplePos = light.origin + sampleOnDisk.x * t0 + sampleOnDisk.y * t1;

        vec3 dir = samplePos - surfacePos;
        dist = length(dir);
        dirToLight = dir / dist;

        vec3 lightNormal = cross(t1, t0); // light forward/default dir is -Z (like UsdLux)
        float cosTheta = max(0.0, dot(-dirToLight, lightNormal));
        float area = radiusXY.x * radiusXY.y * PI;
        invPdf = ((area > 0.0) ? (area * cosTheta) : 1.0) / (dist * dist);

        power = light.baseEmission * PC.lightIntensityMultiplier;
        diffuseSpecularPacked = light.diffuseSpecularPacked;
    }
#endif

    power *= exp2(PC.sensorExposure);
    invPdf *= float(TOTAL_LIGHT_COUNT);
}

void main()
{
    /* 1. Get hit info. */
    vec2 hit_bc = baryCoord;
    float hit_t = gl_HitTEXT;

    /* 2. Set up shading state. */
    State shading_state; // Shading_state_material
    setup_mdl_shading_state(hit_bc, shading_state);

    // we keep a copy of the normal here since it can be changed within the state by *_init() functions:
    // https://github.com/NVIDIA/MDL-SDK/blob/aa9642b2546ad7b6236b5627385d882c2ed83c5d/examples/mdl_sdk/dxr/content/mdl_hit_programs.hlsl#L411
    const vec3 normal = shading_state.normal;

    vec3 throughput = vec3(rayPayload.throughput);
    vec3 radiance = vec3(rayPayload.radiance);

    uint bounce = (rayPayload.bitfield & 0x7FFFu);
    bool inside = bool(rayPayload.bitfield & 0x8000u);

    const bool thin_walled = mdl_thin_walled(shading_state);
    const float ior1 = (inside && !thin_walled) ? BSDF_USE_MATERIAL_IOR : 1.0;
    const float ior2 = (inside && !thin_walled) ? 1.0 : BSDF_USE_MATERIAL_IOR;

#if AOV_ID == AOV_ID_DEBUG_OPACITY
#ifdef IS_OPAQUE
    rayPayload.radiance = vec3(1.0, 0.0, 0.0); // Distinct from viridis heatmap
    rayPayload.bitfield = 0xFFFFu;
#else
    // Payload fields have been set in any-hit shader.
#endif
    return;
#elif AOV_ID == AOV_ID_NORMAL
    rayPayload.radiance = (normal + vec3(1.0, 1.0, 1.0)) * 0.5;
    rayPayload.bitfield = 0xFFFFu;
    return;
#elif AOV_ID == AOV_ID_DEBUG_TANGENTS
    rayPayload.radiance = (shading_state.tangent_u[0] + vec3(1.0, 1.0, 1.0)) * 0.5;
    rayPayload.bitfield = 0xFFFFu;
    return;
#elif AOV_ID == AOV_ID_DEBUG_BITANGENTS
    rayPayload.radiance = (shading_state.tangent_v[0] + vec3(1.0, 1.0, 1.0)) * 0.5;
    rayPayload.bitfield = 0xFFFFu;
    return;
#elif AOV_ID == AOV_ID_DEBUG_BARYCENTRICS
    rayPayload.radiance = vec3(1.0 - hit_bc.x - hit_bc.y, hit_bc.x, hit_bc.y);
    rayPayload.bitfield = 0xFFFFu;
    return;
#elif AOV_ID == AOV_ID_DEBUG_TEXCOORDS
    rayPayload.radiance = vec3(shading_state.text_coords[0]);
    rayPayload.bitfield = 0xFFFFu;
    return;
#endif

    /* 3. Apply volume attenuation */
    if (inside && !thin_walled)
    {
        vec3 abs_coeff = mdl_absorption_coefficient(shading_state);
        throughput.x *= abs_coeff.x > 0.0 ? exp(-abs_coeff.x * hit_t) : 1.0;
        throughput.y *= abs_coeff.y > 0.0 ? exp(-abs_coeff.y * hit_t) : 1.0;
        throughput.z *= abs_coeff.z > 0.0 ? exp(-abs_coeff.z * hit_t) : 1.0;
    }

    /* 4. Add Emission */
    {
        Edf_evaluate_data edf_evaluate_data;
        edf_evaluate_data.k1 = -gl_WorldRayDirectionEXT;
        mdl_edf_emission_init(shading_state);
        mdl_edf_emission_evaluate(edf_evaluate_data, shading_state);

        if (edf_evaluate_data.pdf > 0.0)
        {
            vec3 emission_intensity = mdl_edf_emission_intensity(shading_state);

            emission_intensity *= exp2(PC.sensorExposure);

            radiance += throughput * edf_evaluate_data.edf * emission_intensity;
        }
    }

    // reassign normal, see declaration of variable.
    shading_state.normal = normal;

    // init once and share between *_evaluate and *_sample calls
    mdl_bsdf_scattering_init(shading_state);

    /* 5. NEE light sampling */
#ifdef NEXT_EVENT_ESTIMATION
    if (!inside && TOTAL_LIGHT_COUNT > 0)
    {
        // Sample light source
#ifdef RAND_4D
        vec4 k4 = rng4d_next(rayPayload.rng_state);
#else
        vec4 k4 = vec3(
            rng_next(rayPayload.rng_state),
            rng_next(rayPayload.rng_state),
            rng_next(rayPayload.rng_state),
            rng_next(rayPayload.rng_state)
        );
#endif

        vec3 dirToLight;
        float lightDist;
        vec3 lightPower;
        float invLightSamplePdf;
        uint diffuseSpecularPacked;
        sampleLight(k4, shading_state.position, dirToLight, lightDist, lightPower, invLightSamplePdf, diffuseSpecularPacked);

        vec3 neeContrib = vec3(0.0);
        bool neeValid = (lightDist > 0.0) && dot(dirToLight, shading_state.geom_normal) > 0.0;

        // Calc unshadowed light contribution
        if (neeValid)
        {
            Bsdf_evaluate_data bsdf_eval_data;
            bsdf_eval_data.ior1 = vec3(ior1);
            bsdf_eval_data.ior2 = vec3(ior2);
            bsdf_eval_data.k1 = -gl_WorldRayDirectionEXT;
            bsdf_eval_data.k2 = dirToLight;
            mdl_bsdf_scattering_evaluate(bsdf_eval_data, shading_state);

            neeValid = (bsdf_eval_data.pdf > 0.0);

            if (neeValid)
            {
                vec2 diffuseSpecular = unpackHalf2x16(diffuseSpecularPacked);

                vec3 neeRadiance = lightPower * invLightSamplePdf;

                vec3 weight = throughput * neeRadiance;
                neeContrib += weight * bsdf_eval_data.bsdf_diffuse * diffuseSpecular.x;
                neeContrib += weight * bsdf_eval_data.bsdf_glossy * diffuseSpecular.y;
            }
        }

        rayPayload.neeToLight = dirToLight * lightDist;
        rayPayload.neeContrib = neeContrib;
    }
#endif

    /* 6. Russian Roulette */
#ifdef RAND_4D
    float k1 = rng4d_next(rayPayload.rng_state).x;
#else
    float k1 = rng_next(rayPayload.rng_state);
#endif

    uint maxBounces = PC.maxBouncesAndRrBounceOffset >> 16;
    uint rrBounceOffset = PC.maxBouncesAndRrBounceOffset & 0xFFFFu;

    bool terminatePath = false;
    if (bounce == maxBounces - 1)
    {
        terminatePath = true;
    }
    else if (bounce > rrBounceOffset)
    {
        terminatePath = russian_roulette(k1, throughput);
    }

    /* 7. BSDF (importance) sampling. */
    if (!terminatePath)
    {
        // reassign normal, see declaration of variable.
        shading_state.normal = normal;

        Bsdf_sample_data bsdf_sample_data;
        bsdf_sample_data.ior1 = vec3(ior1);
        bsdf_sample_data.ior2 = vec3(ior2);
        bsdf_sample_data.k1 = -gl_WorldRayDirectionEXT;
#ifdef RAND_4D
        bsdf_sample_data.xi = rng4d_next(rayPayload.rng_state);
#else
        bsdf_sample_data.xi[0] = rng_next(rayPayload.rng_state);
        bsdf_sample_data.xi[1] = rng_next(rayPayload.rng_state);
        bsdf_sample_data.xi[2] = rng_next(rayPayload.rng_state);
        bsdf_sample_data.xi[3] = rng_next(rayPayload.rng_state);
#endif
        mdl_bsdf_scattering_sample(bsdf_sample_data, shading_state);

        terminatePath = (bsdf_sample_data.event_type == BSDF_EVENT_ABSORB);

        bool isTransmission = (bsdf_sample_data.event_type & BSDF_EVENT_TRANSMISSION) != 0;
        if (isTransmission)
        {
            inside = !inside;
        }

        throughput *= bsdf_sample_data.bsdf_over_pdf;

        rayPayload.ray_dir = bsdf_sample_data.k2;
        rayPayload.ray_origin = offset_ray_origin(shading_state.position, shading_state.geom_normal * (isTransmission ? -1.0 : 1.0));
    }

    uint bitfield = (uint(inside) << 15) | (bounce + 1);
    rayPayload.bitfield = terminatePath ? 0xFFFFu : bitfield;
    rayPayload.throughput = throughput;
    rayPayload.radiance = radiance;
}
